set windows-powershell := true

rootdir := ''
prefix := ''
debug := '0'

name := 'android-mic'
appid := 'io.github.teamclouday.' + name

cargo-target-dir := env('CARGO_TARGET_DIR', 'target')
bin-src := cargo-target-dir / if debug == '1' { 'debug' / name } else { 'release' / name }

base-dir := absolute_path(clean(rootdir / prefix))
share-dst := base-dir / 'share'

bin-dst := base-dir / 'bin' / name
desktop-dst := share-dst / 'applications' / appid + '.desktop'
metainfo-dst := share-dst / 'metainfo' / appid + '.metainfo.xml'
icon-dst := share-dst / 'icons/hicolor/scalable/apps' / appid + '.svg'


default: build-release

pull: prettier fix test

build-debug *args:
	cargo build {{args}}

build-release *args:
  cargo build --release {{args}}
	

install: 
	install -Dm0755 {{bin-src}} {{bin-dst}}
	install -Dm0644 res/linux/desktop_entry.desktop {{desktop-dst}}
	install -Dm0644 res/linux/metainfo.xml {{metainfo-dst}}
	install -Dm0644 res/linux/app_icon.svg {{icon-dst}}


uninstall:
	rm {{bin-dst}}
	rm {{desktop-dst}}
	rm {{metainfo-dst}}
	rm {{icon-dst}}

nsis:
	cargo packager --release --formats nsis --verbose

test:
	cargo test --workspace --all-features

fix: fmt
	cargo clippy --workspace --all-features --fix --allow-dirty --allow-staged

fmt:
	cargo fmt --all

prettier:
	# install on Debian: sudo snap install node --classic
	# npx is the command to run npm package, node is the runtime
	npx prettier -w .

metainfo-check:
	appstreamcli validate --pedantic --explain --strict res/linux/metainfo.xml



setup:
    rm -rf flatpak-builder-tools
    git clone https://github.com/flatpak/flatpak-builder-tools
    pip install aiohttp toml

sources-gen:
    python3 flatpak-builder-tools/cargo/flatpak-cargo-generator.py ./Cargo.lock -o cargo-sources.json


sdk-version := "24.08"

install-sdk:
    flatpak remote-add --if-not-exists --user flathub https://flathub.org/repo/flathub.flatpakrepo
    flatpak install --noninteractive --user flathub \
        org.freedesktop.Platform//{{sdk-version}} \
        org.freedesktop.Sdk//{{sdk-version}} \
        org.freedesktop.Sdk.Extension.rust-stable//{{sdk-version}} \
        org.freedesktop.Sdk.Extension.llvm18//{{sdk-version}}

uninstallf:
    flatpak uninstall io.github.teamclouday.android-mic -y || true

# deps: flatpak-builder git-lfs
build-and-install: uninstallf
    rm -rf flatpak-out || true
    flatpak-builder \
        --verbose \
        --ccache \
        --user --install \
        --install-deps-from=flathub \
        --repo=repo \
        flatpak-out \
        io.github.teamclouday.android-mic.json

runf:
    RUST_LOG="warn,android_mic=info" flatpak run io.github.teamclouday.android-mic

# https://github.com/flathub/io.github.wiiznokes.fan-control/blob/master/io.github.wiiznokes.fan-control.json
#Â https://docs.flatpak.org/fr/latest/module-sources.html